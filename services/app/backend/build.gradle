plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'my.portfoliomanager'
version = '0.3.0'
description = 'Portfolio manager for multiple depot types'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation platform('org.testcontainers:testcontainers-bom:2.0.3')
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
	implementation 'tools.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'com.networknt:json-schema-validator:3.0.0'
	implementation 'org.apache.commons:commons-csv:1.14.1'
	implementation 'org.apache.pdfbox:pdfbox:3.0.6'
	implementation 'org.postgresql:postgresql'
	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testCompileOnly 'org.projectlombok:lombok:1.18.36'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jacoco {
	toolVersion = "0.8.13"
}

def coverageExcludes = [
	'**/config/**',
	'**/domain/**',
	'**/dto/**',
	'**/llm/**',
	'**/service/**',
	'**/importer/**',
	'**/api/**'
]

tasks.named('jacocoTestReport') {
	dependsOn tasks.test
	dependsOn tasks.compileJava
	dependsOn tasks.processResources
	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: coverageExcludes)
	}))
	reports {
		xml.required = true
		csv.required = false
		html.required = true
	}
}

tasks.named('jacocoTestCoverageVerification') {
	dependsOn tasks.test
	dependsOn tasks.compileJava
	dependsOn tasks.processResources
	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: coverageExcludes)
	}))
	violationRules {
		rule {
			excludes = coverageExcludes
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
		}
	}
}

tasks.named('check') {
	dependsOn tasks.jacocoTestCoverageVerification
}
